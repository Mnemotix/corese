#
# Generate a table with DBpedia topmost resource updates
# given month/year ?date
#
#
prefix swp: <http://www.w3.org/2004/03/trix/swp-2/>
prefix dbfr:<http://ns.inria.fr/dbpediafr/voc#>
prefix dc:  <http://contextus.net/ontology/ontomedia/misc/date#>
prefix xsd: <http://www.w3.org/2001/xmlschema#>
prefix o:   <http://dbpedia.org/ontology/>

template  st:dbpediafor(?date) {
     
     st:table(st:split(?list, 5))
                  
}
where {
  select (aggregate() as ?list) 
  where {
    bind (st:get(st:date) as ?date) 
    filter kg:slice(100)
    
    service <http://dbpedia-test-fr.inria.fr/historique/sparql> {
    
      select ?x ?c ?i ?date 
      where {
	?rev dc:date ?date 
	?x dbfr:revPerMonth ?rev 
	?rev rdf:value ?c  
	?x o:thumbnail ?i  
	
	  filter (
	      ! regex(?x, "Wikipédia:") 
	    && ! regex(?x, "Utilisateur:") 
	    && ! regex(?x, "Portail:") 
	    && ! regex(?x, "Catégorie:") 
	    && ! regex(?x, "Discussion_") 
	    && ! regex(?x, "Modèle:") 
	    && ! regex(?x, "Projet:")   
	  ) 		

      }
      order by desc(?c)
      limit 50
      
    }
    
    # split in two services appears to be much more efficient !
    
    service <http://dbpedia-test-fr.inria.fr/historique/sparql> {
	?x rdfs:label ?l filter langMatches(lang(?l), "fr")
    }
  }
}


function us:cell(?map){
  let ((?l, ?c, ?x, ?i) = ?map){
    st:format (

	"""	
	<td style='vertical-align: bottom'>
	 %s (%s)
	 <br/>
	  <a href='%s'>
	    <img width='100' alt="%s" src="%s" />
	  </a>
	 
	</td>
	""" ,
	
      
	?l,
	?c,
	st:plink(?x, st:dbpedia),
	?x,
	us:clean(?i) 	
      )
   }
}






